module core;

public type ArrayDeque[T] extends MutableDeque[T] {
  private CircularBuffer[T] backingBuffer;

  public Unit init(Int initialCapacity) {
    this.backingBuffer = new CircularBuffer[T](initialCapacity);
  }

  public Unit init() {
    this.init(4);
  }

  public Unit init(Collection[T] source) {
    this.init(source.size());
    Source[T] enum = source.enumerator();
    #foreach (T elem in source)
    #  this.pushBack(elem);
  }

  public Int size() {
    return this.backingBuffer.size();
  }

  private Int capacity() {
    return this.backingBuffer.capacity();
  }

  public T get(Int index) {
    return this.backingBuffer(index);
  }

  public T set(Int index, T value) {
    return this.backingBuffer(index) = value;
  }

  private Bool atCapacity() {
    return this.size() == this.capacity();
  }

  private Bool wayUnderCapacity() {
    return this.capacity() > 10 & this.size() * 4 < this.capacity();
  }

  private Unit resize(Int newCapacity) {
    this.backingBuffer = new CircularBuffer[T](newCapacity, this.backingBuffer);
  }

  private Unit expand() {
    this.resize(this.capacity() * 2);
  }

  private Unit shrink() {
    this.resize(this.capacity() / 2);
  }

  public Unit pushFront(T value) {
    if (this.atCapacity())
      this.expand();
    this.backingBuffer.pushFront(value);
  }

  public Unit pushBack(T value) {
    if (this.atCapacity())
      this.expand();
    this.backingBuffer.pushBack(value);
  }

  public T popFront() {
    T result = this.backingBuffer.popBack();
    if (this.wayUnderCapacity())
      this.shrink();
    return result;
  }

  public T popBack() {
    T result = this.backingBuffer.popBack();
    if (this.wayUnderCapacity())
      this.shrink();
    return result;
  }

  public Source[T] enumerator() {
    return new RandomAccessEnumerator[T](this);
  }
}
